allprojects {
	repositories {
		mavenCentral()
	}

	group = 'nl.tudelft.sem11b'
	version = '0.1.0'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'
	apply plugin: 'pmd'

	configurations {
		developmentOnly
		runtimeClasspath { extendsFrom developmentOnly }
	}

	dependencies {
		// Jackson JSON serialization/deserialization API bindings
		implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

        // JUnit5 unit testing framework
		testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11) // Could've been 17, just saying. With goodies like:
			                                             //   - Pattern matching
			                                             //   - Text blocks
			                                             //   - Records
			                                             //   - Better NullPointerException
			                                             //   - Sealed classes/iterfaces
		}
	}

	jacoco {
		toolVersion = '0.8.7'
	}

	jacocoTestReport {
		reports {
			xml.required = false
			csv.required = false
			html.outputLocation = file("${buildDir}/reports/jacoco/html")
		}
	}

	jacocoTestCoverageVerification {
		violationRules {
			rule {
				enabled = true
				element = 'CLASS'
				includes = ['nl.tudelft.*']
				
				limit {
					counter = 'LINE'
					value = 'COVEREDRATIO'
					minimum = 0.4
				}
			}
		}
	}

	checkstyle {
		toolVersion = '9.1'		

		configFile = rootProject.file('config/checkstyle.xml')

		ignoreFailures = false
		maxErrors = 0
		maxWarnings = 0
	}

	pmd {
		incrementalAnalysis = true
	}

	test {
		useJUnitPlatform()

		jacoco {
			enabled = true
			includes = ['nl.tudelft.*']
			excludes = []
		}
	}
}
